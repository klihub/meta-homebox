# We have a conf and classes directory, add to BBPATH. Add it at the
# front because we want to override files like fs-perms.txt.
BBPATH =. "${LAYERDIR}:"

# We have recipes-* directories, add to BBFILES
BBFILES += " \
    ${LAYERDIR}/recipes-*/*/*.bb \
    ${LAYERDIR}/recipes-*/*/*.bbappend \
    ${LAYERDIR}/fixes/*/recipes-*/*/*.bb \
    ${LAYERDIR}/fixes/*/recipes-*/*/*.bbappend \
    ${LAYERDIR}/fixes/*/recipes-*/*/*/*.bb \
    ${LAYERDIR}/fixes/*/recipes-*/*/*/*.bbappend \
"

# Set a variable to get to the top of the HomeBox metadata location.
META_HOMEBOX_BASE := '${LAYERDIR}'

BBFILE_COLLECTIONS += "homebox"
BBFILE_PATTERN_homebox := "^${LAYERDIR}/"
BBFILE_PRIORITY_homebox = "6"

# This and the corresponding CONF_VERSION value in local.conf.sample
# need to be increased by one each time a change is made to
# local.conf.sample that requires manually updating a local.conf after
# updating the meta data.
#
# A separate, HomeBox specific variable is used to make the value
# available to derived distros.
HOMEBOX_LOCALCONF_VERSION = "3"
LOCALCONF_VERSION = "${HOMEBOX_LOCALCONF_VERSION}"

# Same for LCONF_VERSION in bblayer.conf.sample.
HOMEBOX_LAYER_CONF_VERSION = "11"
LAYER_CONF_VERSION = "${HOMEBOX_LAYER_CONF_VERSION}"

# The default error messages use shell meta* wildcards to find the
# conf files which in our case happen to match two files (one from
# meta and the real one from meta-homebox). Be more specific to avoid
# user confusion.
SANITY_LOCALCONF_SAMPLE = "${META_HOMEBOX_BASE}/conf/local.conf.sample"
SANITY_BBLAYERCONF_SAMPLE = "${META_HOMEBOX_BASE}/conf/bblayers.conf.sample"

# Corresponds to bblayers.conf.sample.
LAYERDEPENDS_homebox = " \
    core \
    selftest \
    filesystems-layer \
    networking-layer \
    openembedded-layer \
    perl-layer \
    meta-python \
"
